struct camera_information
{
	float4x4 world;
	float4x4 world_projection;
	float4x4 world_it;
};

struct vertex_shader_input
{
	float3 position : POSITION;
	float2 texture_coordinate : TEXCOORD0;
	float3 normal : NORMAL;
};

struct vertex_shader_output
{
	float4 position : POSITION;
	float2 texture_coordinate : TEXCOORD0;
	float3 normal : TEXCOORD1;
	float3 camera_space_position : TEXCOORD2;
	float fog_distance : TEXCOORD3;
};

struct point_light
{
	uniform float4 diffuse_color;
	uniform float4 specular_color;
	uniform float4 ambient_color;
	uniform float3 camera_space_position;
	uniform float3 attenuation;
};

struct directional_light
{
	uniform float4 diffuse_color;
	uniform float4 specular_color;
	uniform float4 ambient_color;
	uniform float3 camera_space_direction;
};

struct material
{
	uniform float4 diffuse_color;
	uniform float4 specular_color;
	uniform float4 ambient_color;
	uniform float4 emissive_color;
	uniform float shininess;
};

struct fog
{
	uniform int enabled;
	uniform float start;
	uniform float end;
	uniform float4 color;
};

vertex_shader_output
vertex_main(
	in vertex_shader_input inputs,
	uniform camera_information camera)
{
	vertex_shader_output result;

	float4 homogenous_position =
		float4(
			inputs.position,
			1.0);

	result.position =
		mul(
			camera.world_projection,
			homogenous_position);

	result.texture_coordinate =
		inputs.texture_coordinate;

	result.camera_space_position =
		mul(
			camera.world,
			homogenous_position).xyz;

	result.fog_distance =
		result.camera_space_position.z;

	result.normal =
		normalize(
			mul(
				camera.world_it,
				float4(
					inputs.normal,
					0.0)).xyz);

	return
		result;
}

float4
pixel_main(
	in vertex_shader_output vertex_shader_out,
	uniform int point_light_count,
	uniform point_light point_lights[MAX_POINT_LIGHTS],
	uniform int directional_light_count,
	uniform directional_light directional_lights[MAX_DIRECTIONAL_LIGHTS],
	uniform material current_material,
	uniform int use_diffuse_texture,
	uniform sampler2D diffuse_texture,
	uniform fog fog_information)
	: COLOR
{
	float4 resulting_color =
		current_material.emissive_color;

	float3 view_direction =
		normalize(
			-vertex_shader_out.camera_space_position);

	for(int i = 0; i < point_light_count; ++i)
	{
		point_light l =
			point_lights[i];

		float3 vertex_to_light =
			l.camera_space_position - vertex_shader_out.camera_space_position;

		float d =
			length(
				vertex_to_light);

		float3 light_direction =
			vertex_to_light / d;

		float3 halfv =
			normalize(
				light_direction + view_direction);

		float4 coeffs =
			lit(
				dot(
					vertex_shader_out.normal,
					light_direction),
				dot(
					vertex_shader_out.normal,
					halfv),
				current_material.shininess.x);


		resulting_color +=
			(l.ambient_color * current_material.ambient_color +
			 l.diffuse_color * current_material.diffuse_color * coeffs.y +
			 l.specular_color * current_material.specular_color * coeffs.z) /
			(l.attenuation.x + d*l.attenuation.y + d*d*l.attenuation.z);
	}

	for(int i = 0; i < directional_light_count; ++i)
	{
		directional_light l =
			directional_lights[i];

		float3 light_direction =
			l.camera_space_direction;

		float3 halfv =
			normalize(
				light_direction + view_direction);

		float4 coeffs =
			lit(
				dot(
					vertex_shader_out.normal,
					light_direction),
				dot(
					vertex_shader_out.normal,
					halfv),
				current_material.shininess.x);

		resulting_color +=
			l.ambient_color * current_material.ambient_color +
			l.diffuse_color * current_material.diffuse_color * coeffs.y +
			l.specular_color * current_material.specular_color * coeffs.z;
	}

	// FIXME: How do we handle transparent objects?
	resulting_color.w = 1.0;

	float4 texture_color =
		use_diffuse_texture
		?
			tex2D(
				diffuse_texture,
				vertex_shader_out.texture_coordinate)
		:
			float4(1.0,1.0,1.0,1.0);

	resulting_color *= texture_color;

	if(fog_information.enabled)
	{
		float fog_factor =
			saturate(
				(fog_information.end - vertex_shader_out.fog_distance) /
				(fog_information.end - fog_information.start));

		resulting_color =
			lerp(
				fog_information.color,
				resulting_color,
				fog_factor);
	}

	return
		resulting_color;
}
